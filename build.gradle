buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://plugins.gradle.org/m2/' }
        maven { url = 'https://repo.spongepowered.org/maven/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle',           name: 'ForgeGradle', version: '5.1+', changing: true
        classpath group: 'org.spongepowered',                   name: 'mixingradle', version: '0.7-SNAPSHOT'
        classpath group: 'gradle.plugin.com.modrinth.minotaur', name: 'Minotaur',    version: '1.2.1'
        classpath group: 'gradle.plugin.com.matthewprenger',    name: 'CurseGradle', version: '1.4.0'
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.matthewprenger.cursegradle'

// This initializes the config. Please try to modify the variables in the config file, if needed, add more if you see a reason to.
ext.config = parseConfig(file('build.properties'))

version = config.VERSION
group = "${config.GROUP}.${config.ARTIFACT}"
archivesBaseName = "${config.ARCHIVES_BASE_NAME}-${config.MINECRAFT_VERSION}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: config.MAPPINGS_CHANNEL, version: config.MAPPINGS_VERSION

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            arg '-mixin.config=secondchanceforge.mixins.json'
            property 'forge.logging.markers', 'SCAN'
            property 'forge.logging.console.level', 'debug'
            mods {
                secondchanceforge {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            arg '-mixin.config=secondchanceforge.mixins.json'
            property 'forge.logging.markers', 'SCAN'
            property 'forge.logging.console.level', 'debug'
            mods {
                secondchanceforge {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'secondchanceforge', '--all', '--output', file('src/generated/resources/')
            mods {
                secondchanceforge {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, 'secondchanceforge.refmap.json'
}

repositories {
    maven { url = 'https://repo.spongepowered.org/maven/' }

    jcenter()
    mavenCentral()
}

dependencies {
    minecraft "net.minecraftforge:forge:${config.MINECRAFT_VERSION}-${config.FORGE_VERSION}"
    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper
processResources {
    doLast {
        fileTree(dir: outputs.files.asPath, include: ['**/*.json', '**/*.mcmeta']).each {
            try {
                it.text = JsonOutput.toJson(new JsonSlurper().parse(it))
            } catch (Exception e) {
                project.logger.error("Failed to minify file '${it.path}'.")
                throw e
            }
        }
    }
}

jar {
    manifest {
        attributes([
                'Specification-Title'   : config.TITLE,
                'Specification-Vendor'  : config.AUTHOR,
                'Specification-Version' : '1',
                'Implementation-Title'  : config.TITLE,
                'Implementation-Version': config.VERSION,
                'Implementation-Vendor' : config.AUTHOR,
                'MixinConfigs'          : 'secondchanceforge.mixins.json'
        ])
    }
}

jar.finalizedBy('reobfJar')

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

tasks.build.dependsOn sourcesJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = config.GROUP
            artifactId = config.ARTIFACT
            artifact jar

            pom {
                name = 'Second Chance (Forge)'
                url = 'https://infernalstudios.org/secondchanceforge'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/infernalexp/Second-Chance-Forge.git'
                    url = 'https://github.com/infernalexp/Second-Chance-Forge'
                }
            }
        }
    }
    repositories {
        maven {
            url 'https://maven.infernalstudios.org/releases'


            credentials {
                username = System.getenv('MAVEN_USER')
                password = System.getenv('MAVEN_TOKEN')
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

// Modrinth publishing
import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload) {
    onlyIf {
        System.getenv('MODRINTH')
    }

    token = System.getenv('MODRINTH')
    projectId = 'H6rTJkIn'
    versionNumber = project.version
    versionName = "Second Chance (Forge) Release ${project.version} for Minecraft 1.19"
    uploadFile = jar
    addGameVersion(config.MINECRAFT_VERSION)
    addLoader('forge')
}

// Curseforge publishing
curseforge {
    apiKey = System.getenv('CURSEFORGE') ?: 'If you\'re seeing this, you haven\'t set your "CURSEFORGE" environment variable'

    project {
        id = '509239'
        changelog = 'No changelog specified.'
        releaseType = 'release'
        addGameVersion '1.19'
        mainArtifact (jar) {
            displayName = "Second Chance (Forge) Release ${project.version} for Minecraft 1.19"
        }
    }
}


// Config parse function
def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

if (System.getProperty("idea.sync.active") == "true") {
    afterEvaluate {
        tasks.withType(JavaCompile).all {
            it.options.annotationProcessorPath = files()
        }
    }
}

